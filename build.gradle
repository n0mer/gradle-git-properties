buildscript {
    dependencies {
        classpath ('org.eclipse.jgit:org.eclipse.jgit:') {
            version {
                strictly '5.13.3.202401111512-r'
            }
        }
    }
}
plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "1.2.1"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
}

repositories {
    mavenCentral()
    maven {
        name = 'ajoberstar-backup'
        url = 'https://ajoberstar.org/bintray-backup/'
    }
    // for debugging
    //maven {
    //    url 'https://repo.gradle.org/gradle/libs-releases-local/'
    //}
}

def integrationTest = sourceSets.create("integrationTest")

dependencies {
    shadow gradleApi()
    shadow localGroovy()
    implementation('org.ajoberstar.grgit:grgit-core:4.1.1') {
        exclude(group: 'org.slf4j')
    }
    // for debugging
    //implementation 'org.gradle:gradle-language-jvm:5.6.2'
    //implementation 'org.gradle:gradle-language-java:5.6.2'


    testImplementation 'junit:junit:4.12'
    testImplementation gradleTestKit()

    integrationTestImplementation 'junit:junit:4.12'
    integrationTestImplementation gradleTestKit()
    integrationTestImplementation project
    integrationTestImplementation sourceSets.test.output // Include test files for git project utils
}

shadowJar {
    archiveClassifier.set('')
    // Relocate bundled dependencies to avoid conflicts with user's dependencies
    relocate 'org.ajoberstar', 'gradlegitproperties.org.ajoberstar'
    relocate 'org.eclipse.jgit', 'gradlegitproperties.org.eclipse.jgit'
    relocate 'com.googlecode.javaewah', 'gradlegitproperties.com.googlecode.javaewah'
}

tasks.shadowJar.dependsOn tasks.jar

components.java.withVariantsFromConfiguration(configurations.runtimeElements) {
    skip()
}

version = "2.5.5"
group = "com.gorylenko.gradle-git-properties"

gitProperties {
    keys = ['git.branch','git.commit.id','git.commit.time']
    dateFormat = "yyyy-MM-dd'T'HH:mm:ssZ"
    dateFormatTimeZone = "PST"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

tasks.register("integrationTest", Test) {
    dependsOn tasks.shadowJar

    description = 'Runs the integration tests.'
    group = "verification"
    testClassesDirs = integrationTest.output.classesDirs
    classpath = integrationTest.runtimeClasspath
    it.systemProperty "integration.plugin.path", tasks.shadowJar.outputs.getFiles().asPath // Pass java system property indicating integration plugin path
}

tasks.register("sourceJar", Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.resources, sourceSets.main.groovy

    dependsOn tasks.named("classes")
}

publishing {

    publications {

        pluginJar(MavenPublication) {  publication ->

            from project.shadow.component(publication)

            artifact sourceJar
        }
    }
    // for testing: ./gradlew clean publishToMavenLocal
    repositories {
        mavenLocal()
    }
}

gradlePlugin {
    website = 'http://github.com/n0mer/gradle-git-properties'
    vcsUrl = 'https://github.com/n0mer/gradle-git-properties'

    plugins {
        gitPropertiesPlugin {
            id = 'com.gorylenko.gradle-git-properties'
            implementationClass = 'com.gorylenko.GitPropertiesPlugin'
            displayName = 'Gradle Git Properties plugin'
            description = 'Produce git.properties for spring-boot-actuator'
            tags.set(['git', 'spring-boot'])
        }
    }
}

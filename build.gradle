buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.9"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.0.0"
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.16'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: 'maven-publish'
apply plugin: 'groovy'

repositories {
    jcenter()
    // for debugging
    //maven {
    //    url 'https://repo.gradle.org/gradle/libs-releases-local/'
    //}
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar.grgit:grgit-core:4.0.1'
    // for debugging
    //compile 'org.gradle:gradle-language-jvm:5.6.2'
    //compile 'org.gradle:gradle-language-java:5.6.2'


    testCompile 'junit:junit:4.12'
}

version = "2.2.1"
group = "com.gorylenko.gradle-git-properties"

gitProperties {
    keys = ['git.branch','git.commit.id','git.commit.time']
    dateFormat = "yyyy-MM-dd'T'HH:mm:ssZ"
    dateFormatTimeZone = "PST"
}

tasks.withType(GroovyCompile) {
    targetCompatibility = JavaVersion.VERSION_1_8
}
test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}
publishing {

    publications {

        pluginJar(MavenPublication) {

            groupId project.group
            artifactId jar.baseName

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    // for testing: ./gradlew clean publishToMavenLocal
    //repositories {
    //    mavenLocal()
    //}
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.resources, sourceSets.main.groovy
}

pluginBundle {
    website = 'http://github.com/n0mer/gradle-git-properties'
    vcsUrl = 'https://github.com/n0mer/gradle-git-properties'
    description = 'Produce git.properties for spring-boot-actuator'
    tags = ['git', 'spring-boot']

    plugins {
        gitPropertiesPlugin {
            id = 'com.gorylenko.gradle-git-properties'
            displayName = 'Gradle Git Properties plugin'
        }
    }
}
